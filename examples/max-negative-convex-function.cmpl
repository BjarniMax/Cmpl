# 
# Calculation of the maximum of a negative convex function 
# f(x) := -0.5 * x^2 -e^-x
#
parameters:
	#golden ratio delta
	d:=0.382;
	#distance epsilon
	e:=0.0001;	
		
	#initial solution
	a= 0;
	b= 1;
	
	l:= a + d*(b-a);
	m:= a+ (1-d)*(b-a);	
	
	Fl:= -0.5 * l^2 - exp(-l);
	Fm:= -0.5 * m^2 - exp(-m);	
		
	{ (b-a)>=e :
		{ Fl<Fm: 
			a:=l;
			l:=m;
			m:=a+(1-d)*(b-a);
			Fl:=Fm;
			Fm:=-0.5 * m^2 - exp(-m);
		}
		{ Fl>=Fm:
			b:=m;
			m:=l;
			l:=a+d*(b-a);
			Fm:=Fl;
			Fl:= -0.5 * l^2 - exp(-l);
		}
		repeat;
	}
	echo "Optimal solution found";
	x:=round(b*1000)/1000;
	echo "x: "+ format("%2.3lf",x); 
	echo "function value: " + (-0.5 * x^2 - exp(-x));
